# =================================================================================================
# Device settings
# =================================================================================================
variable "hostname" {
  type        = string
  description = "The name to assign to this device."
}

variable "timezone" {
  type        = string
  default     = "Europe/Warsaw"
  description = "The timezone to set on the device."
}

variable "disable_ipv6" {
  type        = bool
  default     = true
  description = "Whether to disable IPv6 on the device."
}

variable "ntp_servers" {
  type        = list(string)
  default     = ["time.cloudflare.com"]
  description = "List of NTP servers to use."
}

# =================================================================================================
# Management access
# =================================================================================================
variable "mac_server_interfaces" {
  type        = string
  default     = "all"
  description = "Interface list to allow MAC server access on."
}

# =================================================================================================
# Certificate details
# =================================================================================================
variable "certificate_common_name" {
  type        = string
  description = "CN for the device certificate."
}

variable "certificate_country" {
  type        = string
  default     = "PL"
  description = "Country code for the device certificate."
}

variable "certificate_locality" {
  type        = string
  default     = "STC"
  description = "Locality for the device certificate."
}

variable "certificate_organization" {
  type        = string
  default     = "VZKN"
  description = "Organization for the device certificate."
}

variable "certificate_unit" {
  type        = string
  default     = "HOME"
  description = "Organizational unit for the device certificate."
}


# =================================================================================================
# Bridge settings
# =================================================================================================
variable "bridge_name" {
  type        = string
  default     = "bridge"
  description = "Name of the main bridge interface"
}

variable "bridge_comment" {
  type        = string
  default     = ""
  description = "Comment for the bridge interface"
}

variable "bridge_mtu" {
  type        = number
  default     = 1514
  description = "MTU for the bridge interface. If null, defaults to 1514."
}


# =================================================================================================
# VLAN Configuration
# =================================================================================================
variable "vlans" {
  type = map(object({
    name        = string
    vlan_id     = number
    network     = string
    cidr_suffix = string
    gateway     = string
    dhcp_pool   = list(string)
    dns_servers = list(string)
    domain      = string
    mtu         = optional(number, 1500)
    static_leases = map(object({
      name = string
      mac  = string
    }))
  }))
  default     = {}
  description = "Map of VLANs to configure"
}


# =================================================================================================
# Interface Configuration
# =================================================================================================
variable "ethernet_interfaces" {
  type = map(object({
    comment     = optional(string, "")
    bridge_port = optional(bool, true)
    l2mtu       = optional(number, 1514) # Layer 2 MTU
    mtu         = optional(number, 1500) # Layer 3 MTU

    # VLAN configurations
    tagged   = optional(list(string)) # list of VLAN names
    untagged = optional(string)       # VLAN name for untagged traffic
  }))
  default     = {}
  description = "Map of ethernet interfaces to configure"
}

variable "bond_interfaces" {
  type = map(object({
    comment              = optional(string, "")
    slaves               = list(string)
    mode                 = optional(string, "802.3ad")       # 802.3ad, balance-rr, balance-xor, broadcast, active-backup, balance-tlb, balance-alb
    transmit_hash_policy = optional(string, "layer-2-and-3") # layer-2, layer-2-and-3, layer-3-and-4
    mtu                  = optional(number, 1500)            # MTU for the bond interface

    # VLAN configurations
    tagged   = optional(list(string))
    untagged = optional(string)
  }))
  default     = {}
  description = "Map of bond interfaces to configure"
}


# =================================================================================================
# BGP Configuration
# =================================================================================================
variable "bgp_enabled" {
  type        = bool
  default     = false
  description = "Enable BGP routing"
}

variable "bgp_instance" {
  type = object({
    name      = string
    as        = number
    router_id = string
  })
  default     = null
  description = "BGP instance configuration"
}

variable "bgp_peer_connections" {
  type = map(object({
    name             = string
    remote_address   = string
    remote_as        = number
    local_address    = string
    address_families = optional(string, "ip")
    multihop         = optional(bool, false)
  }))
  default     = {}
  description = "Map of BGP peer connections to other routers/switches"
}

variable "bgp_k8s_peers" {
  type = map(object({
    ip = string
  }))
  default     = {}
  description = "Map of Kubernetes node BGP peers"
}

variable "bgp_k8s_asn" {
  type        = number
  default     = null
  description = "ASN for Kubernetes nodes"
}


# =================================================================================================
# DHCP Client Configuration
# =================================================================================================
variable "dhcp_clients" {
  type = map(object({
    interface              = string
    comment                = optional(string, "")
    add_default_route      = optional(string, "no")
    default_route_distance = optional(number, 1)
    disabled               = optional(bool, false)
    dhcp_options           = optional(string, "hostname,clientid")
    use_peer_dns           = optional(bool, false)
    use_peer_ntp           = optional(bool, false)
  }))
  default     = {}
  description = "Map of DHCP clients to configure"
}
